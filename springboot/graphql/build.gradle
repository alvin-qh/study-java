plugins {
  id "org.flywaydb.flyway"
}

/**
 * 配置 Spring Boot 插件
 */
springBoot {
  // 设置入口类
  mainClass = "alvin.study.springboot.graphql.Main"
}

/**
 * 配置 Spring Boot 打包插件
 */
bootJar {
  exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

  // 配置 MANIFEST.MF 文件内容
  manifest {
    attributes 'Implementation-Title': 'Study Spring Boot GraphQL'
    attributes 'Implementation-Version': projectVersion.version
  }

  // 配置打包 jar 文件信息
  archiveBaseName = project.group
  archiveFileName = "alvin.study.springboot-graphql.${archiveExtension.get()}"
}

/**
 * 配置 Flyway 插件, 用于 DB Migration
 */
flyway {
  url = "jdbc:h2:${projectDir}/.data/dev;MODE=MYSQL"
  user = "dev"
  password = "password"
  locations = [
    "filesystem:${projectDir}/src/main/resources/migration"
  ]
  table = "schema_version"
}

dependencies {
  implementation "org.springframework.boot:spring-boot-starter-graphql",
                 "org.springframework.boot:spring-boot-starter-web",
                 "org.springframework.boot:spring-boot-starter-jdbc",
                 "org.springframework.boot:spring-boot-starter-validation",
                 "com.baomidou:mybatis-plus-jsqlparser:${VER_MYBATIS_PLUS}",
                 "com.graphql-java:graphql-java-extended-scalars:${VER_GRAPHQL_EXTENDED}",
                 "com.graphql-java:graphql-java-extended-validation:${VER_GRAPHQL_EXTENDED}",
                 "io.micrometer:context-propagation:${VAR_CONTEXT_PROPAGATION}",
                 "org.modelmapper:modelmapper:${VER_MODELMAPPER}",
                 "commons-codec:commons-codec:${VER_COMMONS_CODEC}",
                 "com.h2database:h2:${VER_H2_DB}"
  implementation ("com.baomidou:mybatis-plus-spring-boot3-starter:${VER_MYBATIS_PLUS}") {
    exclude group: "org.springframework.boot", module: "*"
  }
  implementation ("com.auth0:java-jwt:${VER_AUTH0_JWT}") {
    exclude group: "com.fasterxml.jackson.core", module: "*"
  }
  runtimeOnly "org.flywaydb:flyway-core:${VER_FLYWAY}"
  testImplementation "org.springframework.graphql:spring-graphql-test:${VER_GRAPHQL_SPRING_TEST}",
                     "org.springframework.boot:spring-boot-starter-webflux",
                     "org.mockito:mockito-core:${VER_MOCKITO}",
                     "org.mockito:mockito-junit-jupiter:${VER_MOCKITO}",
                     "p6spy:p6spy:${VER_P6SPY}"
}
