plugins {
  id "org.springframework.boot"
  id "org.flywaydb.flyway"
}

dependencies {
  implementation libs.main.springboot.shiro,
    libs.main.springboot.web,
    libs.main.springboot.mybatisPlus,
    libs.main.springboot.redis,
    libs.main.springboot.validator,
    libs.main.h2,
    libs.main.commonsCodec,
    libs.main.commonsPool2,
    libs.main.jwt,
    libs.main.modelmapper

  developmentOnly libs.main.springboot.devtools

  testImplementation libs.test.springboot.web,
    libs.test.springboot.security

  runtimeOnly libs.main.flyway
}

springBoot {
  mainClass = "alvin.study.springboot.shiro.Main"
}

bootJar {
  exclude "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA", "META-INF/*.MF"

  manifest {
    attributes "Implementation-Title": "Study Spring Boot Shiro"
    attributes "Implementation-Version": projectVersion.version
  }

  archiveBaseName = project.group
  archiveFileName = "alvin.study.springboot-shiro.${archiveExtension.get()}"
}

flyway {
  url = "jdbc:h2:${projectDir}/.data/dev;MODE=MYSQL"
  user = "dev"
  password = "password"
  locations = [
    "filesystem:${projectDir}/src/main/resources/migration"
  ]
  table = "schema_version"
}
