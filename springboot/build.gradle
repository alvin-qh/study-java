plugins {
  id "org.springframework.boot" version "${VER_SPRING_BOOT}" apply false
  id "io.spring.dependency-management" version "${VER_GRADLE_SPRING_DEP_MGR}" apply false
}

group "alvin.study.springboot"
version "1.0-SNAPSHOT"

subprojects {
  apply plugin: "org.springframework.boot"

  if (!new File(projectDir, "src").exists()) {
    return
  }

  apply plugin: "io.spring.dependency-management"

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-dependencies:${VER_SPRING_BOOT}"
    }
  }

  configurations {
    providedRuntime
    all*.exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
  }

  dependencies {
    implementation "org.springframework.boot:spring-boot-starter",
                   "org.springframework.boot:spring-boot-starter-log4j2",
                   "com.google.guava:guava:${VER_GUAVA}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    developmentOnly "org.springframework.boot:spring-boot-devtools:${VER_SPRING_BOOT}"
    testImplementation "org.springframework.boot:spring-boot-starter-test",
                       "org.assertj:assertj-core:${VER_ASSERTJ}"
  }
}
